plugins {
    // defines which gradle plugin to use for building this module
    id 'com.android.application'
    id 'kotlin-android'
    // generates classes so you can safely switch between fragments
    // no passing of resource ids or bundles, can use arguments directly
    id 'androidx.navigation.safeargs.kotlin'
    // adds the room-compiler as a gradle plugin
    id 'kotlin-kapt'
}

android {
    compileSdk 31

    // generates Java classes for each layout
    // layout classes can be used directly in code without referencing xml resources
    buildFeatures {
        viewBinding true
    }

    // exclude license files to avoid collisions
    packagingOptions {
        exclude 'META-INF/AL2.0'
        exclude 'META-INF/LGPL2.1'
    }

    defaultConfig {
        // saves different versions of room data base schema
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        applicationId "com.example.todo"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        // allows for use of java.time classes on older devices
        // transforming bytecode (desugaring) to reference other (bytecode) implementation instead
        // "To support these language APIs, the plugin compiles a separate DEX file that contains an
        // implementation of the missing APIs and includes it in your app"
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    // handles annotations at compile time
    kapt "androidx.room:room-compiler:$room_version"
    // allows for use of java.time classes on older devices
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation 'com.google.android.material:material:1.4.0'
    // dependency inversion (injection)
    implementation "io.insert-koin:koin-android:$koin_version"
    // coroutines for room
    implementation "androidx.room:room-ktx:$room_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    // preferences of jetpack, used to build app settings UI (uses shared prefs)
    implementation "androidx.preference:preference-ktx:1.2.0"

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    // used for testing views
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.1'
    testImplementation 'junit:junit:4.13.2'

    testImplementation "org.mockito:mockito-inline:3.12.1"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.1'
}